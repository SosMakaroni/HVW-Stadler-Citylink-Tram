grf {
	grfid: "HS\16\01";
	name: string(STR_GRF_NAME);
	desc: string(STR_GRF_DESC);
	version: 10;
	min_compatible_version: 5722;
	param 0 {
        param_build {
            type: int;
            name: string(STR_PARAM_BUY_NAME);
            desc: string(STR_PARAM_BUY_DESC);
            min_value: 0;
            max_value: 4;
            def_value: 2;
            names: {
					0: string(STR_PARAM_GAME_VERY_EASY);
                    1: string(STR_PARAM_GAME_EASY);
					2: string(STR_PARAM_GAME_NORMAL);
					3: string(STR_PARAM_GAME_HARD);
					4: string(STR_PARAM_GAME_VERY_HARD);
                    };
				}
			}
	param 1 {
		param_maintenance {
            type: int;
            name: string(STR_PARAM_MAINTENANCE_NAME);
            desc: string(STR_PARAM_MAINTENANCE_DESC);
            min_value: 0;
            max_value: 4;
            def_value: 2;
            names: {
					0: string(STR_PARAM_GAME_VERY_EASY);
                    1: string(STR_PARAM_GAME_EASY);
					2: string(STR_PARAM_GAME_NORMAL);
					3: string(STR_PARAM_GAME_HARD);
					4: string(STR_PARAM_GAME_VERY_HARD);
                    };
				}
			}

}

template tmpl_purchase() {
	[0,	0,	100,	16,	-25,-9]}
	
template tmpl_depo() {
	[0,	20,	250,	16,	-15,-10]}
	
template tmpl_1(z1){
	[0		*z1,	0,	11	*z1,	49	*z1,	-5	*z1,	-13	*z1,	ANIM]
	[19		*z1,	0,	54	*z1,	40	*z1,	-45	*z1,	-12	*z1,	ANIM]
	[81		*z1,	0,	69	*z1,	15	*z1,	-54	*z1,	-10	*z1,	ANIM]
	[158	*z1,	0,	54	*z1,	40	*z1,	-39	*z1,	-28	*z1,	ANIM]
	[220	*z1,	0,	11	*z1,	49	*z1,	-6	*z1,	-32	*z1,	ANIM]
	[239	*z1,	0,	54	*z1,	40	*z1,	-15	*z1,	-28	*z1,	ANIM]
	[301	*z1,	0,	69	*z1,	15	*z1,	-11	*z1,	-9	*z1,	ANIM]
	[378	*z1,	0,	54	*z1,	40	*z1,	-8	*z1,	-14	*z1,	ANIM]}

template tmpl_toldat() {
	[0,	0,	2,	2,	1,	1]}	

spriteset(ss_toldat, "src/icons.png")		{tmpl_toldat()}

//Wood logo, replaces ł 0x0142
font_glyph (NORMAL, 0x0142, "src/icons.png") {[20,80,24,10,0,0]}
font_glyph (SMALL, 0x0142, "src/icons.png") {[10,80,1,1,0,0]}
font_glyph (LARGE, 0x0142, "src/icons.png") {[10,80,1,1,0,0]}
font_glyph (MONO, 0x0142, "src/icons.png") {[10,80,1,1,0,0]}

//Trailer logo, replaces Ł 0x0141
font_glyph (NORMAL, 0x0141, "src/icons.png") {[60,80,29,10,0,0]}
font_glyph (SMALL, 0x0141, "src/icons.png") {[10,80,1,1,0,0]}
font_glyph (LARGE, 0x0141, "src/icons.png") {[10,80,1,1,0,0]}
font_glyph (MONO, 0x0141, "src/icons.png") {[10,80,1,1,0,0]}

//Double logo, replaces đ 0x0111
font_glyph (NORMAL, 0x0111, "src/icons.png") {[110,80,29,10,0,0]}
font_glyph (SMALL, 0x0111, "src/icons.png") {[10,80,1,1,0,0]}
font_glyph (LARGE, 0x0111, "src/icons.png") {[10,80,1,1,0,0]}
font_glyph (MONO, 0x0111, "src/icons.png") {[10,80,1,1,0,0]}

//Triple logo, replaces Đ 0x0110
font_glyph (NORMAL, 0x0110, "src/icons.png") {[160,80,25,10,0,0]}
font_glyph (SMALL, 0x0110, "src/icons.png") {[10,80,1,1,0,0]}
font_glyph (LARGE, 0x0110, "src/icons.png") {[10,80,1,1,0,0]}
font_glyph (MONO, 0x0110, "src/icons.png") {[10,80,1,1,0,0]}

//Twin logo, replaces Ń 0x0143
font_glyph (NORMAL, 0x0143, "src/icons.png") {[20,110,21,10,0,0]}
font_glyph (SMALL, 0x0143, "src/icons.png") {[10,80,1,1,0,0]}
font_glyph (LARGE, 0x0143, "src/icons.png") {[10,80,1,1,0,0]}
font_glyph (MONO, 0x0143, "src/icons.png") {[10,80,1,1,0,0]}
// --------------------------------------------------------------------------------Citylink

// ss=spriteset, sg=spritegroup, sw=switch
// tmpl_1(szin,csuklo,rakodas,poziciok)
// tmpl_depo(szin,csuklo)

spriteset(ss_Citylink_purchase, "src/CityLinka1_depo.png")		{tmpl_purchase()}
//--------------A
spriteset(ss_Citylink_1_a_depo, "src/CityLinka1_depo.png")		{tmpl_depo()}
spriteset(ss_Citylink_1_a, "src/CityLinka1_1x_8bpp.png")		{tmpl_1(1)}
spriteset(ss_Citylink_2_a, "src/CityLinka2_1x_8bpp.png")		{tmpl_1(1)}
spriteset(ss_Citylink_3_a, "src/CityLinka3_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_Citylink_1_a, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/CityLinka1_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_Citylink_2_a, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/CityLinka2_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_Citylink_3_a, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/CityLinka3_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_Citylink_1_a, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/CityLinka1_4x_8bpp.png") {tmpl_1(4)}
alternative_sprites(ss_Citylink_2_a, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/CityLinka2_4x_8bpp.png") {tmpl_1(4)}
alternative_sprites(ss_Citylink_3_a, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/CityLinka3_4x_8bpp.png") {tmpl_1(4)}

spritegroup sg_Citylink_1_a {	loaded:  [ss_Citylink_1_a];	loading: [ss_Citylink_1_a];}
spritegroup sg_Citylink_2_a {	loaded:  [ss_Citylink_2_a];	loading: [ss_Citylink_2_a];}
spritegroup sg_Citylink_3_a {	loaded:  [ss_Citylink_3_a];	loading: [ss_Citylink_3_a];}
//--------------A end
//--------------B
spriteset(ss_Citylink_1_b_depo, "src/CityLinkb1_depo.png")		{tmpl_depo()}
spriteset(ss_Citylink_1_b, "src/CityLinkb1_1x_8bpp.png")		{tmpl_1(1)}
spriteset(ss_Citylink_2_b, "src/CityLinkb2_1x_8bpp.png")		{tmpl_1(1)}
spriteset(ss_Citylink_3_b, "src/CityLinkb3_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_Citylink_1_b, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/CityLinkb1_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_Citylink_2_b, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/CityLinkb2_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_Citylink_3_b, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/CityLinkb3_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_Citylink_1_b, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/CityLinkb1_4x_8bpp.png") {tmpl_1(4)}
alternative_sprites(ss_Citylink_2_b, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/CityLinkb2_4x_8bpp.png") {tmpl_1(4)}
alternative_sprites(ss_Citylink_3_b, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/CityLinkb3_4x_8bpp.png") {tmpl_1(4)}

spritegroup sg_Citylink_1_b {	loaded:  [ss_Citylink_1_b];	loading: [ss_Citylink_1_b];}
spritegroup sg_Citylink_2_b {	loaded:  [ss_Citylink_2_b];	loading: [ss_Citylink_2_b];}
spritegroup sg_Citylink_3_b {	loaded:  [ss_Citylink_3_b];	loading: [ss_Citylink_3_b];}
//--------------B end
//--------------C
spriteset(ss_Citylink_1_c_depo, "src/CityLinkc1_depo.png")		{tmpl_depo()}
spriteset(ss_Citylink_1_c, "src/CityLinkc1_1x_8bpp.png")		{tmpl_1(1)}
spriteset(ss_Citylink_2_c, "src/CityLinkc2_1x_8bpp.png")		{tmpl_1(1)}
spriteset(ss_Citylink_3_c, "src/CityLinkc3_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_Citylink_1_c, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/CityLinkc1_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_Citylink_2_c, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/CityLinkc2_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_Citylink_3_c, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/CityLinkc3_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_Citylink_1_c, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/CityLinkc1_4x_8bpp.png") {tmpl_1(4)}
alternative_sprites(ss_Citylink_2_c, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/CityLinkc2_4x_8bpp.png") {tmpl_1(4)}
alternative_sprites(ss_Citylink_3_c, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/CityLinkc3_4x_8bpp.png") {tmpl_1(4)}

spritegroup sg_Citylink_1_c {	loaded:  [ss_Citylink_1_c];	loading: [ss_Citylink_1_c];}
spritegroup sg_Citylink_2_c {	loaded:  [ss_Citylink_2_c];	loading: [ss_Citylink_2_c];}
spritegroup sg_Citylink_3_c {	loaded:  [ss_Citylink_3_c];	loading: [ss_Citylink_3_c];}
//--------------C end

//--------------A
switch (FEAT_ROADVEHS, SELF, ss_Citylink_a_depo, position_in_consist ) {
    1..5: ss_toldat;
    ss_Citylink_1_a_depo;
}
switch (FEAT_ROADVEHS, SELF, sw_Citylink_a, position_in_consist ) {
    1: ss_toldat;
	2: sg_Citylink_2_a;
	3: ss_toldat;
	4: sg_Citylink_3_a;
	5: ss_toldat;
    sg_Citylink_1_a;
}
switch(FEAT_ROADVEHS, SELF, sw_Citylink_a_ss, (extra_callback_info1 & 0x12)) {
   0x10..0x12: ss_Citylink_a_depo; // 0x10 Depot GUI, 0x11 Vehicle Details/Refit GUI, 0x12 Vehicle List
   0x00: sw_Citylink_a; // 0x00 Vehicle in game
}
//--------------A end
//--------------B
switch (FEAT_ROADVEHS, SELF, ss_Citylink_b_depo, position_in_consist ) {
    1..5: ss_toldat;
    ss_Citylink_1_b_depo;
}
switch (FEAT_ROADVEHS, SELF, sw_Citylink_b, position_in_consist ) {
    1: ss_toldat;
	2: sg_Citylink_2_b;
	3: ss_toldat;
	4: sg_Citylink_3_b;
	5: ss_toldat;
    sg_Citylink_1_b;
}
switch(FEAT_ROADVEHS, SELF, sw_Citylink_b_ss, (extra_callback_info1 & 0x12)) {
   0x10..0x12: ss_Citylink_b_depo; // 0x10 Depot GUI, 0x11 Vehicle Details/Refit GUI, 0x12 Vehicle List
   0x00: sw_Citylink_b; // 0x00 Vehicle in game
}
//--------------B end
//--------------C
switch (FEAT_ROADVEHS, SELF, ss_Citylink_c_depo, position_in_consist ) {
    1..5: ss_toldat;
    ss_Citylink_1_c_depo;
}
switch (FEAT_ROADVEHS, SELF, sw_Citylink_c, position_in_consist ) {
    1: ss_toldat;
	2: sg_Citylink_2_c;
	3: ss_toldat;
	4: sg_Citylink_3_c;
	5: ss_toldat;
    sg_Citylink_1_c;
}
switch(FEAT_ROADVEHS, SELF, sw_Citylink_c_ss, (extra_callback_info1 & 0x12)) {
   0x10..0x12: ss_Citylink_c_depo; // 0x10 Depot GUI, 0x11 Vehicle Details/Refit GUI, 0x12 Vehicle List
   0x00: sw_Citylink_c; // 0x00 Vehicle in game
}
//--------------C end
switch (FEAT_ROADVEHS, SELF, sw_Citylink_ss, cargo_subtype) {
	0: sw_Citylink_a_ss;
	1: sw_Citylink_b_ss;	
	2: sw_Citylink_c_ss;	
	CB_RESULT_NO_TEXT;
}
switch (FEAT_ROADVEHS, SELF, sw_Citylink_articulated, extra_callback_info1) {
    0..5: item_Citylink;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch (FEAT_ROADVEHS, SELF, sw_Citylink_capacity, position_in_consist) {
	1: return 0;
	2: return 72;
	3: return 0;
	4: return 72;	
	5: return 0;
	return 72;
}
switch (FEAT_ROADVEHS, SELF, sw_Citylink_length, position_in_consist) {
	1: return 4;
	2: return 8;
	3: return 2;
	4: return 5;
	5: return 8;
    return 8;
}
switch (FEAT_ROADVEHS, SELF, sw_Citylink_livery_text, cargo_subtype) {
	0: return string(STR_COLOUR_SCHEME_SZEGED);
	1: return string(STR_COLOUR_SCHEME_CHEMNITZ);	
	2: return string(STR_COLOUR_SCHEME_KARLSRUHE);	
	CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_Citylink) {
	property {
        name:								string(STR_Citylink_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(2021,01,01);
        model_life:						25;
        vehicle_life:						40;
        reliability_decay:				10;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					20;
        cost_factor:						100;
        running_cost_factor:				100;
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								100 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								580 kW;
        weight:							58 ton;
        cargo_capacity:					216;
		cargo_age_period:					20;
        sound_effect:						SOUND_ELECTRIC_SPARK;
    }
    graphics {
		default:                 		sw_Citylink_ss;
		purchase:						ss_Citylink_purchase;
		articulated_part:        		sw_Citylink_articulated;
		length:                  		sw_Citylink_length;
		cargo_subtype_text: 			sw_Citylink_livery_text;
		cargo_capacity: 				sw_Citylink_capacity;
		purchase_cargo_capacity:		return 36;
		additional_text: 				string(STR_Citylink_ADAT);
    }
}
// --------------------------------------------------------------------------------Citylink End
sort(FEAT_ROADVEHS, [
	item_Citylink
	]);